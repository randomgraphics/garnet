Import( 'GN' )

# ******************
# define build flags
# ******************

ccflags = []
cppdefines = []

if 'icl' == GN.conf['compiler']:
    unimportant_warnings = [
        '1572', # floating-point comparison
        ]
    ccflags += ['/W3'] + ['/Qwd%s'%w for w in unimportant_warnings]
elif 'vc71' == GN.conf['compiler']:
    ccflags += ['/W3']
elif 'vc80' == GN.conf['compiler']:
    ccflags += ['/W3']
    cppdefines += Split('_CRT_SECURE_NO_DEPRECATE _CRT_NONSTDC_NO_DEPRECATE')
elif 'vc80-x64' == GN.conf['compiler']:
    unimportant_warnings = [
        '4334', # 32-bit shift convert to 64-bit
        '4267', # conversion from 'size_t' to xxx, possible loss of data
        '4244', # conversion from 'type1' to 'type2', possible loss of data
        ]
    ccflags += ['/W3'] + ['/wd%s'%w for w in unimportant_warnings]
    cppdefines += Split('_CRT_SECURE_NO_DEPRECATE _CRT_NONSTDC_NO_DEPRECATE')

# **********************
# define source clusters
# **********************
jpeg_cluster = GN.newSourceCluster( GN.glob( 'jpeg/*.c' ) )
if 'gcc' == GN.conf['compiler']: jpeg_cluster.addCompileFlags( CCFLAGS = ['-Wno-main'] )
jpeg_cluster.addCompileFlags( CPPPATH=['jpeg'], CCFLAGS = ccflags, CPPDEFINES = cppdefines )
jpeg_cluster.removeCompileFlags( CCFLAGS = ['/W4'] )

common_sources = GN.glob( Split('lua/*.c zlib/*.c png/*.c pcre/*.c* tinyxml/*.cpp freetype2/*.c'), True )
if GN.conf['has_ogl']: common_sources += GN.glob( 'glew/*.c' )
common_cluster = GN.newSourceCluster( common_sources )
common_cluster.addCompileFlags( CCFLAGS = ccflags, CPPDEFINES = cppdefines )
common_cluster.removeCompileFlags( CCFLAGS = ['/W4'] )

# **********************
# define source clusters
# **********************
lib = GN.newTarget( 'stlib', 'GNextern', [jpeg_cluster,common_cluster] )
if 'win32' == GN.conf['platform']:
    lib.externalDependencies = ['glu32','opengl32']
else:
    lib.externalDependencies = ['GLU','GL','X11']
