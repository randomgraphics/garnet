Import('GN env compiler variant name target')

# local functions
def toRelativePath( files ):
    return [ GN.relpath( File(x).srcnode().path, Dir(target_dir).srcnode().path ) for x in files ]
def getMsvsPlatform( env ) :
	if 'ppc' == compiler.cpu:
		return 'Xbox 360'
	elif 'x64' == compiler.cpu:
		return 'x64'
	elif 'x86' == compiler.cpu:
		return 'Win32'
	else:
		GN.error( "Unknown target CPU: %s"%(compiler.cpu) )
		Exit(-1);

import os.path

# determine target directory and variant string
target_dir = env['MSVS_VERSION']
variant_str = '%s|%s'%(variant,getMsvsPlatform(env))

# build target directory
absdir = Dir(target_dir).srcnode().abspath
if not os.path.exists( absdir ) : os.makedirs( absdir )

# compose command line arguments
cmdargs = 'variant=%s compiler=%s os=%s cpu=%s'%(variant,compiler.name, compiler.os, compiler.cpu)

# collect source files
sources = []
for x in target.sources : sources += x.sources
sources += GN.glob( '%s/*.[hi]*'%target.path, True )

# collect header files
incs = []
if 'GNbase' == name:
    incs = GN.glob( '#src/priv/inc/*.[hi]*', True )
elif 'GNextern' == name:
    incs = GN.glob( '#src/extern/inc/*.[hi]*', True )

# do build
proj = env.MSVSProject(
    target      = os.path.join( target_dir, name ) + env['MSVSPROJECTSUFFIX'],
    buildtarget = target.targets[0],
    variant     = variant_str,
    cmdargs     = cmdargs,
    srcs        = toRelativePath( sources ),
    incs        = toRelativePath( incs ),
    localincs   = [],
    resources   = [],
    misc        = [],
    auto_build_solution = 0,
    )
AlwaysBuild( proj )
Alias( 'msvc', proj )
