Import('env GN_targets GN_conf')

import os.path

env = env.Copy()

if 'vc80-x64' == GN_conf['compiler']:
    variant = '%s|x64'%GN_conf['variant']
    target_dir = '8.0'
elif 'vc80' == GN_conf['compiler']:
    variant = '%s|Win32'%GN_conf['variant']
    target_dir = '8.0'
else:
    variant = '%s|Win32'%GN_conf['variant']
    target_dir = '7.1'

absdir = Dir(target_dir).srcnode().abspath
if not os.path.exists( absdir ) : os.makedirs( absdir )

cmdargs = 'variant=%s compiler=%s'%(GN_conf['variant'],GN_conf['compiler'])

#
#
# -----------------------------------------------------------------------------
def to_relpath( files ):
    return [ env.GN_relpath( File(x).path, Dir(target_dir).srcnode().path ) for x in files ]

#
#
# -----------------------------------------------------------------------------
def build_proj( target, srcdir=[], incdir=[], moduleName='' ):
    if not target in GN_targets: return

    sources = env.GN_glob( ['%s/*.[c]*'%x for x in srcdir], True )
    if 0 == len(incdir):
        if '' == moduleName: moduleName = target
        includes = env.GN_glob( '#src/priv/inc/garnet/%s.h'%moduleName )
        includes += env.GN_glob( '#src/priv/inc/garnet/%s/*.[hi]*'%moduleName[2:], True )
    else:
        includes = env.GN_glob( ['%s/*.[hi]*'%x for x in incdir], True )
    localincs = env.GN_glob( ['%s/*.[hi]*'%x for x in srcdir], True )

    proj = env.MSVSProject(
        target      = os.path.join( target_dir, target ) + env['MSVSPROJECTSUFFIX'],
        buildtarget = GN_targets[target][0],
        variant     = variant,
        cmdargs     = cmdargs,
        srcs        = to_relpath( sources ),
        incs        = to_relpath( includes ),
        localincs   = to_relpath( localincs ),
        resources   = [],
        misc        = [],
        auto_build_solution = 0,
        )

    env.AlwaysBuild( proj );

###############################################################################
#
# Build project for garnet components
#
###############################################################################

build_proj( 'GNextern', ['#src/extern/src'], ['#src/extern/inc'] )
build_proj( 'GNbase', ['#src/priv/base'] )
build_proj( 'GNcore', ['#src/priv/core'] )
build_proj( 'GNgfxLib', ['#src/priv/gfx/lib'], [], 'GNgfx' )
build_proj( 'GNgfxCommon', ['#src/priv/gfx/renderer/common'], [], 'GNgfx' )
build_proj( 'GNgfxD3D', ['#src/priv/gfx/renderer/d3d'], [], 'GNgfx' )
build_proj( 'GNgfxOGL', ['#src/priv/gfx/renderer/ogl'], [], 'GNgfx' )
build_proj( 'GNinput', ['#src/priv/input'] )
build_proj( 'GNwin', ['#src/priv/win'] )
build_proj( 'GNut', ['#src/priv/test/ut'] )
build_proj( 'GNgfxTest', ['#src/priv/test/gfx'] )
build_proj( 'GNinputTest', ['#src/priv/test/input'] )
build_proj( 'GNd3dTest', ['#src/priv/test/d3d'] )
build_proj( 'GNoglTest', ['#src/priv/test/ogl'] )
build_proj( 'GNwinTest', ['#src/priv/test/win'] )
build_proj( 'GNsampleCommon', ['#src/priv/sample/common'] )
build_proj( 'GNdepthTexture', ['#src/priv/sample/depthTexture'] )
build_proj( 'GNrenderToTexture', ['#src/priv/sample/renderToTexture'] )
