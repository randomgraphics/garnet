Import('env GN_targets GN_conf GN_func')

import os.path

#
#
# -----------------------------------------------------------------------------
def to_relpath( files ):
    return [ GN_func['relpath']( File(x).path, Dir('.').srcnode().path ) for x in files ]

#
#
# -----------------------------------------------------------------------------
def build_proj( target, srcdir=[], incdir=['#core/priv/inc'] ):
    if not target in GN_targets: return

    sources = GN_func['glob']( '*.[c]*', srcdir, True )
    includes = GN_func['glob']( '*.[hi]*', incdir, True )
    localincs = GN_func['glob']( '*.[hi]*', srcdir, True )

    proj = env.MSVSProject(
        target      = target + env['MSVSPROJECTSUFFIX'],
        buildtarget = GN_targets[target],
        variant     = GN_conf['variant'],
        srcs        = to_relpath(sources),
        incs        = to_relpath(includes),
        localincs   = to_relpath(localincs),
        resources   = [],
        misc        = [],
        )

    env.AlwaysBuild( proj );

###############################################################################
#
# Build project for garnet components
#
###############################################################################

build_proj( 'GNextern', ['#core/extern/src'], ['#core/extern/inc'] )
build_proj( 'GNbase', ['#core/priv/base'] )
build_proj( 'GNcore', ['#core/priv/core'] )
build_proj( 'GNd3d', ['#core/priv/d3d'] )
build_proj( 'GNd3dTest', ['#core/priv/d3d/test'] )
build_proj( 'GNogl', ['#core/priv/ogl'] )
build_proj( 'GNoglTest', ['#core/priv/ogl/test'] )
build_proj( 'GNtest', ['#core/priv/test'] )

